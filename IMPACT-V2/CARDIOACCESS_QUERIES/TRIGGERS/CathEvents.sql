/*
PENDING IMPORT ID LEGEND
	0 = IMPORT SUCCESSFUL
	1 = SOURCE RECORD WAS UPDATED AND WAITING FOR DATA UPLOAD TO DESTINATION DB
	2 = NO MATCHING CASE EXISTS IN DESTINATION DB (CASES.EMREventID)
	3 = ERROR UPDATING DESTINATION RECORD
	4 = ERROR INSERTING DESTINATION RECORD
*/

CREATE TRIGGER TR_CHOP_IMPACT_CATHEVENTS ON CHOP_IMPACT_CATHEVENTS
AFTER UPDATE, INSERT

AS

DECLARE @i int 
DECLARE @numrows int 
DECLARE @EventID int, @DBVrsn varchar(5), @EMREventID int
DECLARE @CathCaseList TABLE (CathID int, EMREventID int, DBVrsn varchar(5), RowID int)
DECLARE @ErrMsg nvarchar(4000), @ErrSeverity int


							
	SET NOCOUNT ON 
	
	INSERT INTO @CathCaseList(CathID, EMREventID, DBVrsn, RowID)
	SELECT CathID, CATH_SRC.SURG_ENC_ID, h.IMPACTDataVrsn , ROW_NUMBER() OVER (ORDER BY CathID) 
	FROM [dbo].[CHOP_IMPACT_CATHEVENTS] CATH_SRC INNER JOIN dbo.CathData C ON CATH_SRC.SURG_ENC_ID = C.EMREventID 
	                                                 INNER JOIN dbo.Hospitalization H on C.HospID = H.HospitalizationID
	WHERE PendingImport IN (1,2)   --This is to test an individual record
	--and EMREVENTID in (2059350625)
	ORDER BY CathID
		
	SELECT @numrows = @@RowCount, @i = 1
	WHILE (@i <= @numrows) 
		BEGIN
			SELECT @EventID = CathID, @DBVrsn = DBVrsn, @EMREventID = EMREventID FROM @CathCaseList AC WHERE AC.RowID = @i

			IF EXISTS(SELECT ASD.CathID FROM CATHEVENTS ASD WHERE ASD.CathID = @EventID)  
				BEGIN
					BEGIN TRY
					   BEGIN
						--BEGIN THE TRANSACTION
						BEGIN TRAN
							--UPDATE THE TARGET RECORD
							UPDATE CATH_TRGT
							SET CATH_TRGT.CARREST=CATH_SRC.CARREST,
								CATH_TRGT.POSTARRHYTH=CATH_SRC.POSTARRHYTH,
								CATH_TRGT.POSTARRHYTHMED=CATH_SRC.POSTARRHYTHMED,
								CATH_TRGT.POSTARRHYTHCARDIOVERS=CATH_SRC.POSTARRHYTHCARDIOVERS,
								CATH_TRGT.POSTARRHYTHTEMPPM=CATH_SRC.POSTARRHYTHTEMPPM,
								CATH_TRGT.POSTARRHYTHPERMPM=CATH_SRC.POSTARRHYTHPERMPM,
								CATH_TRGT.POSTNEWREGURGE=CATH_SRC.POSTNEWREGURGE,
								CATH_TRGT.POSTTAMPONADE=CATH_SRC.POSTTAMPONADE,
								CATH_TRGT.POSTAIREMBOLUS=CATH_SRC.POSTAIREMBOLUS,
								CATH_TRGT.POSTEMBSTROKE=CATH_SRC.POSTEMBSTROKE,
								CATH_TRGT.POSTDEVMALPOSTHROM=CATH_SRC.POSTDEVMALPOSTHROM,
								CATH_TRGT.POSTDEVEMBOL=CATH_SRC.POSTDEVEMBOL,
								CATH_TRGT.POSTDEVRETRIEVEPCT=CATH_SRC.POSTDEVRETRIEVEPCT,
								CATH_TRGT.POSTDEVRETRIEVESURG=CATH_SRC.POSTDEVRETRIEVESURG,
								CATH_TRGT.POSTDIALYSIS=CATH_SRC.POSTDIALYSIS,
								CATH_TRGT.POSTINTUBATION=CATH_SRC.POSTINTUBATION,
								CATH_TRGT.POSTECMO=CATH_SRC.POSTECMO,
								CATH_TRGT.POSTLVAD=CATH_SRC.POSTLVAD,
								CATH_TRGT.POSTBLEED=CATH_SRC.POSTBLEED,
								CATH_TRGT.POSTBLEEDACCESSSITE=CATH_SRC.POSTBLEEDACCESSSITE,
								CATH_TRGT.POSTBLEEDHEMATOMA=CATH_SRC.POSTBLEEDHEMATOMA,
								CATH_TRGT.POSTRETROBLEED=CATH_SRC.POSTRETROBLEED,
								CATH_TRGT.POSTGIBLEED=CATH_SRC.POSTGIBLEED,
								CATH_TRGT.POSTGUBLEED=CATH_SRC.POSTGUBLEED,
								CATH_TRGT.POSTOTHERBLEED=CATH_SRC.POSTOTHERBLEED,
								CATH_TRGT.POSTTRANSFUSION=CATH_SRC.POSTTRANSFUSION,
								CATH_TRGT.POSTOTHERVASCOMP=CATH_SRC.POSTOTHERVASCOMP,
								CATH_TRGT.POSTOTHEREVENTS=CATH_SRC.POSTOTHEREVENTS,
								CATH_TRGT.POSTPLANCARDIACSURG=CATH_SRC.POSTPLANCARDIACSURG,
								CATH_TRGT.POSTUNPLANCARDSURG=CATH_SRC.POSTUNPLANCARDSURG,
								CATH_TRGT.POSTUNPLANVASSURG=CATH_SRC.POSTUNPLANVASSURG,
								CATH_TRGT.POSTUNPLANOTHERSURG=CATH_SRC.POSTUNPLANOTHERSURG,
								CATH_TRGT.POSTOTHERSURGCATHCOMP=CATH_SRC.POSTOTHERSURGCATHCOMP,
								CATH_TRGT.POSTSUBSCATH=CATH_SRC.POSTSUBSCATH,
								CATH_TRGT.POSTAVBLOCK=CATH_SRC.POSTAVBLOCK,
								CATH_TRGT.POSTARRHYTHRESOLVED=CATH_SRC.POSTARRHYTHRESOLVED,
								CATH_TRGT.POSTCORARTERYCOMP=CATH_SRC.POSTCORARTERYCOMP,
								CATH_TRGT.POSTEROSION=CATH_SRC.POSTEROSION,
								CATH_TRGT.POSTESOFISTULA=CATH_SRC.POSTESOFISTULA,
								CATH_TRGT.POSTLBBB=CATH_SRC.POSTLBBB,
								CATH_TRGT.POSTRBBB=CATH_SRC.POSTRBBB,
								CATH_TRGT.POSTDROPHGB=CATH_SRC.POSTDROPHGB,
								CATH_TRGT.POSTPRIORANEMIA=CATH_SRC.POSTPRIORANEMIA,
								CATH_TRGT.POSTBLOODLOSS=CATH_SRC.POSTBLOODLOSS,
								CATH_TRGT.POSTECMOBLOODREPLACE=CATH_SRC.POSTECMOBLOODREPLACE,
								CATH_TRGT.POSTPERINERVEINJURY=CATH_SRC.POSTPERINERVEINJURY,
								CATH_TRGT.POSTPHNERVEPARALYSIS=CATH_SRC.POSTPHNERVEPARALYSIS,
								CATH_TRGT.POSTPNEUMOTHORAX=CATH_SRC.POSTPNEUMOTHORAX,
								CATH_TRGT.POSTPULEMBOLISM=CATH_SRC.POSTPULEMBOLISM,
								CATH_TRGT.POSTPULVEINSTENOSIS=CATH_SRC.POSTPULVEINSTENOSIS,
								CATH_TRGT.POSTRADIATIONBURN=CATH_SRC.POSTRADIATIONBURN,
								CATH_TRGT.POSTDVT=CATH_SRC.POSTDVT,
								CATH_TRGT.POSTCONDUITTEAR=CATH_SRC.POSTCONDUITTEAR,
								CATH_TRGT.POSTCONDUITTEARLOC=CATH_SRC.POSTCONDUITTEARLOC,
								CATH_TRGT.POSTCONDUITTEARTREAT=CATH_SRC.POSTCONDUITTEARTREAT
			
							FROM CathEvents CATH_TRGT INNER JOIN @CathCaseList AC ON CATH_TRGT.CathID = AC.CathID  
							INNER JOIN [dbo].[CHOP_IMPACT_CATHEVENTS] CATH_SRC ON AC.EMREventID = CATH_SRC.SURG_ENC_ID  
							WHERE AC.RowID = @i
							
							--UPDATE THE SOURCE RECORD RESETTING THE PENDING IMPORT FLAG TO 0
							UPDATE CATH_SRC 
							SET CATH_SRC.PendingImport = 0
							FROM [dbo].[CHOP_IMPACT_CATHEVENTS] CATH_SRC INNER JOIN @CathCaseList AC ON CATH_SRC.SURG_ENC_ID = AC.EMREventID  
							WHERE AC.RowID = @i
						
						COMMIT TRAN
						--COMMIT THE PENDING TRANSACTION IF NO ERRORS
						
						-- VALIDATE THE RECORD FOR THE END USER
						EXEC Validation_Call_ByTableEventID 'CATHEVENTS',@EventID,4,@DBVrsn;
					  END	
					END	TRY
					--IF ERROR IN TRANSACTION, PROVIDE ROLLBACK PROCEDURE
					BEGIN CATCH
						--PRINT 'BEGIN CATCH'
						IF @@TRANCOUNT > 0
							BEGIN
								--PRINT 'BEGIN ROLLBACK'
								
								ROLLBACK TRAN
								
								--PRINT 'BEGIN ERROR UPDATE'
								
								UPDATE CATH_SRC 
								SET CATH_SRC.PendingImport = 3
								FROM [dbo].[CHOP_IMPACT_CATHEVENTS] CATH_SRC INNER JOIN @CathCaseList AC ON CATH_SRC.SURG_ENC_ID = AC.EMREventID  
								WHERE AC.RowID = @i
								
								--PRINT 'END ERROR UPDATE'
								
								SELECT @ErrMsg = ERROR_MESSAGE(), @ErrSeverity = ERROR_SEVERITY()
								
								RAISERROR(@ErrMsg, @ErrSeverity, 1)
							END
					END CATCH
					
				END
			ELSE IF EXISTS(SELECT C.CathID FROM CathData C INNER JOIN @CathCaseList AC ON C.CathID = AC.CathID WHERE AC.RowID = @i)
				
				BEGIN TRY
				  BEGIN
					BEGIN TRAN
						INSERT INTO dbo.CATHEVENTS (CATHID, CARREST,POSTAIREMBOLUS,POSTARRHYTH,POSTARRHYTHCARDIOVERS,POSTARRHYTHMED,POSTARRHYTHPERMPM,POSTARRHYTHRESOLVED,POSTARRHYTHTEMPPM,POSTAVBLOCK,POSTBLEED,POSTBLEEDACCESSSITE,POSTBLEEDHEMATOMA,POSTBLOODLOSS,POSTCONDUITTEAR,POSTCONDUITTEARLOC,POSTCONDUITTEARTREAT,POSTCORARTERYCOMP,POSTDEVEMBOL,POSTDEVMALPOSTHROM,POSTDEVRETRIEVEPCT,POSTDEVRETRIEVESURG,POSTDIALYSIS,POSTDROPHGB,POSTDVT,POSTECMO,POSTECMOBLOODREPLACE,POSTEMBSTROKE,POSTEROSION,POSTESOFISTULA,POSTGIBLEED,POSTGUBLEED,POSTINTUBATION,POSTLBBB,POSTLVAD,POSTNEWREGURGE,POSTOTHERBLEED,POSTOTHEREVENTS,POSTOTHERSURGCATHCOMP,POSTOTHERVASCOMP,POSTPERINERVEINJURY,POSTPHNERVEPARALYSIS,POSTPLANCARDIACSURG,POSTPNEUMOTHORAX,POSTPRIORANEMIA,POSTPULEMBOLISM,POSTPULVEINSTENOSIS,POSTRADIATIONBURN,POSTRBBB,POSTRETROBLEED,POSTSUBSCATH,POSTTAMPONADE,POSTTRANSFUSION,POSTUNPLANCARDSURG,POSTUNPLANOTHERSURG,POSTUNPLANVASSURG) 
						SELECT AC.CathID,CARREST,POSTAIREMBOLUS,POSTARRHYTH,POSTARRHYTHCARDIOVERS,POSTARRHYTHMED,POSTARRHYTHPERMPM,POSTARRHYTHRESOLVED,POSTARRHYTHTEMPPM,POSTAVBLOCK,POSTBLEED,POSTBLEEDACCESSSITE,POSTBLEEDHEMATOMA,POSTBLOODLOSS,POSTCONDUITTEAR,POSTCONDUITTEARLOC,POSTCONDUITTEARTREAT,POSTCORARTERYCOMP,POSTDEVEMBOL,POSTDEVMALPOSTHROM,POSTDEVRETRIEVEPCT,POSTDEVRETRIEVESURG,POSTDIALYSIS,POSTDROPHGB,POSTDVT,POSTECMO,POSTECMOBLOODREPLACE,POSTEMBSTROKE,POSTEROSION,POSTESOFISTULA,POSTGIBLEED,POSTGUBLEED,POSTINTUBATION,POSTLBBB,POSTLVAD,POSTNEWREGURGE,POSTOTHERBLEED,POSTOTHEREVENTS,POSTOTHERSURGCATHCOMP,POSTOTHERVASCOMP,POSTPERINERVEINJURY,POSTPHNERVEPARALYSIS,POSTPLANCARDIACSURG,POSTPNEUMOTHORAX,POSTPRIORANEMIA,POSTPULEMBOLISM,POSTPULVEINSTENOSIS,POSTRADIATIONBURN,POSTRBBB,POSTRETROBLEED,POSTSUBSCATH,POSTTAMPONADE,POSTTRANSFUSION,POSTUNPLANCARDSURG,POSTUNPLANOTHERSURG,POSTUNPLANVASSURG
						FROM [dbo].[CHOP_IMPACT_CATHEVENTS] CATH_SRC INNER JOIN @CathCaseList AC ON AC.EMREventID = CATH_SRC.SURG_ENC_ID 
						WHERE AC.RowID = @i

						UPDATE CATH_SRC 
						SET CATH_SRC.PendingImport = 0
						FROM [dbo].[CHOP_IMPACT_CATHEVENTS] CATH_SRC INNER JOIN @CathCaseList AC ON CATH_SRC.SURG_ENC_ID = AC.EMREventID  
						WHERE AC.RowID = @i
					COMMIT TRAN
					--COMMIT THE PENDING TRANSACTION IF NO ERRORS
					
					-- VALIDATE THE RECORD FOR THE END USER
					EXEC Validation_Call_ByTableEventID 'CathEvents',@EventID,4,@DBVrsn;
					EXEC Validation_Call_ByTableEventID 'CathEventOther' ,@EventID,4,@DBVrsn;
                   END
				END	TRY
				BEGIN CATCH
					IF @@TRANCOUNT > 0
						BEGIN
							ROLLBACK TRAN
							
							UPDATE CATH_SRC 
							SET CATH_SRC.PendingImport = 4
							FROM [dbo].[CHOP_IMPACT_CATHEVENTS] CATH_SRC INNER JOIN @CathCaseList AC ON CATH_SRC.SURG_ENC_ID = AC.EMREventID 
							WHERE AC.RowID = @i
							
							SELECT @ErrMsg = ERROR_MESSAGE(), @ErrSeverity = ERROR_SEVERITY()
							RAISERROR(@ErrMsg, @ErrSeverity, 1)

							
						END						
				END CATCH
			ELSE
				BEGIN
					UPDATE CATH_SRC 
					SET CATH_SRC.PendingImport = 2
					FROM [dbo].[CHOP_IMPACT_CATHEVENTS] CATH_SRC INNER JOIN @CathCaseList AC ON CATH_SRC.SURG_ENC_ID = AC.EMREventID  
					WHERE AC.RowID = @i
					 
				END

			SET @i = @i + 1
		END
