/*
PENDING IMPORT ID LEGEND
	0 = IMPORT SUCCESSFUL
	1 = SOURCE RECORD WAS UPDATED AND WAITING FOR DATA UPLOAD TO DESTINATION DB
	2 = NO MATCHING CASE EXISTS IN DESTINATION DB (CASES.EMREventID)
	3 = ERROR UPDATING DESTINATION RECORD
	4 = ERROR INSERTING DESTINATION RECORD
*/

CREATE TRIGGER TR_CHOP_IMPACT_CATHCOARCPROC ON CHOP_IMPACT_CATHCOARCPROC
AFTER UPDATE, INSERT

AS

DECLARE @i int 
DECLARE @numrows int 
DECLARE @EventID int, @DBVrsn varchar(5), @EMREventID int
DECLARE @CathCaseList TABLE (CathID int, EMREventID int, DBVrsn varchar(5), RowID int)
DECLARE @ErrMsg nvarchar(4000), @ErrSeverity int


							
	SET NOCOUNT ON 
	
	INSERT INTO @CathCaseList(CathID, EMREventID, DBVrsn, RowID)
	SELECT CathID, CATH_SRC.SURG_ENC_ID, h.IMPACTDataVrsn , ROW_NUMBER() OVER (ORDER BY CathID) 
	FROM [dbo].[CHOP_IMPACT_CATHCOARCPROC] CATH_SRC INNER JOIN dbo.CathData C ON CATH_SRC.SURG_ENC_ID = C.EMREventID 
	                                                 INNER JOIN dbo.Hospitalization H on C.HospID = H.HospitalizationID
	WHERE PendingImport IN (1,2)   --This is to test an individual record
	--and SURG_ENC_ID = 2058689001
	ORDER BY CathID
		
	SELECT @numrows = @@RowCount, @i = 1
	WHILE (@i <= @numrows) 
		BEGIN
			SELECT @EventID = CathID, @DBVrsn = DBVrsn, @EMREventID = EMREventID FROM @CathCaseList AC WHERE AC.RowID = @i

			IF EXISTS(SELECT ASD.CathID FROM CATHCOARCPROC ASD WHERE ASD.CathID = @EventID)  
				BEGIN
					BEGIN TRY
					   BEGIN
						--BEGIN THE TRANSACTION
						BEGIN TRAN
							--UPDATE THE TARGET RECORD
							UPDATE CATH_TRGT
							SET 
			                     CATH_TRGT.COARCPROCIND=CATH_SRC.COARCPROCIND,
								CATH_TRGT.COARCPREDIAMETER=CATH_SRC.COARCPREDIAMETER,
								CATH_TRGT.COARCPREPKSYSGRAD=CATH_SRC.COARCPREPKSYSGRAD,
								CATH_TRGT.COARCDEFECTTREATED=CATH_SRC.COARCDEFECTTREATED,
								CATH_TRGT.COARCPOSTDIAMETER=CATH_SRC.COARCPOSTDIAMETER,
								CATH_TRGT.COARCPOSTPKSYSGRAD=CATH_SRC.COARCPOSTPKSYSGRAD,
								CATH_TRGT.COARCNATURE=CATH_SRC.COARCNATURE,
								CATH_TRGT.COARCPRIORTREAT=CATH_SRC.COARCPRIORTREAT,
								CATH_TRGT.COARCADDLAORTOBS=CATH_SRC.COARCADDLAORTOBS,
								CATH_TRGT.COARCAORTICARCHINTER=CATH_SRC.COARCAORTICARCHINTER,
								CATH_TRGT.COARCPRESYSGRADIENT=CATH_SRC.COARCPRESYSGRADIENT,
								CATH_TRGT.COARCPOSTSYSGRADIENT=CATH_SRC.COARCPOSTSYSGRADIENT,
								CATH_TRGT.COARCPREDIAMETERNA=CATH_SRC.COARCPREDIAMETERNA,
								CATH_TRGT.COARCPREPKSYSGRADNA=CATH_SRC.COARCPREPKSYSGRADNA,
								CATH_TRGT.COARCPOSTDIAMETERNA=CATH_SRC.COARCPOSTDIAMETERNA,
								CATH_TRGT.COARCPOSTPKSYSGRADNA=CATH_SRC.COARCPOSTPKSYSGRADNA	
							FROM CATHCOARCPROC CATH_TRGT INNER JOIN @CathCaseList AC ON CATH_TRGT.CathID = AC.CathID  
							INNER JOIN [dbo].[CHOP_IMPACT_CATHCOARCPROC] CATH_SRC ON AC.EMREventID = CATH_SRC.SURG_ENC_ID  
							WHERE AC.RowID = @i
							
							--UPDATE THE SOURCE RECORD RESETTING THE PENDING IMPORT FLAG TO 0
							UPDATE CATH_SRC 
							SET CATH_SRC.PendingImport = 0
							FROM [dbo].[CHOP_IMPACT_CATHCOARCPROC] CATH_SRC INNER JOIN @CathCaseList AC ON CATH_SRC.SURG_ENC_ID = AC.EMREventID  
							WHERE AC.RowID = @i
						
						COMMIT TRAN
						--COMMIT THE PENDING TRANSACTION IF NO ERRORS
						
						-- VALIDATE THE RECORD FOR THE END USER
						EXEC Validation_Call_ByTableEventID 'CATHCOARCPROC',@EventID,4,@DBVrsn;
					  END	
					END	TRY
					--IF ERROR IN TRANSACTION, PROVIDE ROLLBACK PROCEDURE
					BEGIN CATCH
						--PRINT 'BEGIN CATCH'
						IF @@TRANCOUNT > 0
							BEGIN
								--PRINT 'BEGIN ROLLBACK'
								
								ROLLBACK TRAN
								
								--PRINT 'BEGIN ERROR UPDATE'
								
								UPDATE CATH_SRC 
								SET CATH_SRC.PendingImport = 3
								FROM [dbo].[CHOP_IMPACT_CATHCOARCPROC] CATH_SRC INNER JOIN @CathCaseList AC ON CATH_SRC.SURG_ENC_ID = AC.EMREventID  
								WHERE AC.RowID = @i
								
								--PRINT 'END ERROR UPDATE'
								
								SELECT @ErrMsg = ERROR_MESSAGE(), @ErrSeverity = ERROR_SEVERITY()
								
								RAISERROR(@ErrMsg, @ErrSeverity, 1)
							END
					END CATCH
					
				END
			ELSE IF EXISTS(SELECT C.CathID FROM CathData C INNER JOIN @CathCaseList AC ON C.CathID = AC.CathID WHERE AC.RowID = @i)
				
				BEGIN TRY
				  BEGIN
					BEGIN TRAN
						INSERT INTO dbo.CATHCOARCPROC (CathID,CoarcAddlAortObs,CoarcAorticArchInter,CoarcDefectTreated,CoarcNature,CoarcPostDiameter,CoarcPostDiameterNA,CoarcPostPkSysGrad,CoarcPostPkSysGradNA,CoarcPostSysGradient,CoarcPreDiameter,CoarcPreDiameterNA,CoarcPrePkSysGrad,CoarcPrePkSysGradNA,CoarcPreSysGradient,CoarcPriorTreat,CoarcProcInd) 
						SELECT AC.CathID,CoarcAddlAortObs,CoarcAorticArchInter,CoarcDefectTreated,CoarcNature,CoarcPostDiameter,CoarcPostDiameterNA,CoarcPostPkSysGrad,CoarcPostPkSysGradNA,CoarcPostSysGradient,CoarcPreDiameter,CoarcPreDiameterNA,CoarcPrePkSysGrad,CoarcPrePkSysGradNA,CoarcPreSysGradient,CoarcPriorTreat,CoarcProcInd
						FROM [dbo].[CHOP_IMPACT_CATHCOARCPROC] CATH_SRC INNER JOIN @CathCaseList AC ON AC.EMREventID = CATH_SRC.SURG_ENC_ID 
						WHERE AC.RowID = @i

						UPDATE CATH_SRC 
						SET CATH_SRC.PendingImport = 0
						FROM [dbo].[CHOP_IMPACT_CATHCOARCPROC] CATH_SRC INNER JOIN @CathCaseList AC ON CATH_SRC.SURG_ENC_ID = AC.EMREventID  
						WHERE AC.RowID = @i
					COMMIT TRAN
					--COMMIT THE PENDING TRANSACTION IF NO ERRORS
					
					-- VALIDATE THE RECORD FOR THE END USER
					EXEC Validation_Call_ByTableEventID 'CATHCOARCPROC',@EventID,4,@DBVrsn;
                   END
				END	TRY
				BEGIN CATCH
					IF @@TRANCOUNT > 0
						BEGIN
							ROLLBACK TRAN
							
							UPDATE CATH_SRC 
							SET CATH_SRC.PendingImport = 4
							FROM [dbo].[CHOP_IMPACT_CATHCOARCPROC] CATH_SRC INNER JOIN @CathCaseList AC ON CATH_SRC.SURG_ENC_ID = AC.EMREventID 
							WHERE AC.RowID = @i
							
							SELECT @ErrMsg = ERROR_MESSAGE(), @ErrSeverity = ERROR_SEVERITY()
							RAISERROR(@ErrMsg, @ErrSeverity, 1)

							
						END						
				END CATCH
			ELSE
				BEGIN
					UPDATE CATH_SRC 
					SET CATH_SRC.PendingImport = 2
					FROM [dbo].[CHOP_IMPACT_CATHCOARCPROC] CATH_SRC INNER JOIN @CathCaseList AC ON CATH_SRC.SURG_ENC_ID = AC.EMREventID  
					WHERE AC.RowID = @i
					 
				END

			SET @i = @i + 1
		END
